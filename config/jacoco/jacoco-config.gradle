def jacocoTaskName = "jacocoTestReport-staging-unified"

tasks.register(jacocoTaskName, JacocoReport) {
    dependsOn 'testStagingDebugUnitTest'

    reports {
        xml.required = true
        html.required = true
    }

    def excludes = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            'android/**/*.*',
            '**/*$ViewBinding*.*'
    ]

    def javaClasses = fileTree(dir: "${project.buildDir}/intermediates/javac/stagingDebug/classes", excludes: excludes)
    def kotlinClasses = fileTree(dir: "${project.buildDir}/tmp/kotlin-classes/stagingDebug", excludes: excludes)

    classDirectories.from = files([javaClasses, kotlinClasses])
    sourceDirectories.from = files([
            "${project.projectDir}/src/main/java",
            "${project.projectDir}/src/main/kotlin",
            "${project.projectDir}/src/staging/java",
            "${project.projectDir}/src/staging/kotlin",
            "${project.projectDir}/src/debug/java",
            "${project.projectDir}/src/debug/kotlin"
    ])
    executionData.from = files("${project.buildDir}/outputs/unit_test_code_coverage/stagingDebugUnitTest/testStagingDebugUnitTest.exec")
}